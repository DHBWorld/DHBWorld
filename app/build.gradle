plugins {
    id 'com.android.application'
}

apply plugin: 'com.google.gms.google-services'

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = file("$buildDir/reports/coverage")
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoCombinedTestReports(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    group = "Verification"
    description = "Creates JaCoCo test coverage report for Unit and Instrumented Tests (combined) on the Debug build"
    reports {
        xml.required = true
        html.required = true
    }

    // Files to exclude:
    // Generated classes, platform classes, etc.
    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            'androidx/**/*.*',
            'com/google/**/*.*',
            '**/databinding/*.*'
    ]

    classDirectories.from(fileTree(dir: "$project.buildDir/intermediates/javac/debug/classes", excludes: excludes))
    // sources
    sourceDirectories.from = [
            android.sourceSets.main.java.srcDirs,
            "src/main/kotlin"
    ]
    // Output and existing data
    // Combine Unit test and Instrumented test reports
    executionData.from = fileTree(dir: "$buildDir", includes: [
            // Unit tests coverage data
            "outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec",
            // Instrumented tests coverage data
            "outputs/code_coverage/debugAndroidTest/connected/*coverage.ec"
    ])
}

tasks.register('printVersionName') {
    doLast {
        println android.defaultConfig.versionName
    }
}

tasks.register('printVersionCode') {
    doLast {
        println android.defaultConfig.versionCode
    }
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.main.dhbworld"
        minSdk 26
        targetSdk 33
        versionCode 22
        versionName "3.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'com.main.dhbworld'

}

dependencies {
    implementation(fileTree(dir: 'libs', include: ['*.jar']))

    implementation platform('com.google.firebase:firebase-bom:29.0.0')

    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'com.github.thellmund.Android-Week-View:core:5.3.2'
    implementation 'androidx.navigation:navigation-fragment:2.5.3' // DO NOT UPDATE, BREAKS EVERYTHING
    implementation 'androidx.navigation:navigation-ui:2.5.3' // DO NOT UPDATE, BREAKS EVERYTHING
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.work:work-runtime:2.8.1'
    implementation 'com.google.firebase:firebase-firestore:24.4.2' // DO NOT UPDATE, BREAKS EVERYTHING


    testImplementation 'junit:junit:4.13.2'
    debugImplementation 'androidx.test.ext:junit:1.1.5'
    debugImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    debugImplementation 'androidx.test:core:1.5.0'

    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-database'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.code.gson:gson:2.9.0'

    implementation 'org.jsoup:jsoup:1.14.3'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'androidx.work:work-runtime:2.8.1'

    implementation 'com.jsibbold:zoomage:1.3.1'

    implementation 'commons-io:commons-io:2.11.0'

    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    implementation 'org.kohsuke:github-api:1.315'
}